apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply from: '../scripts/jacoco.gradle'
apply from: '../scripts/keys.gradle'

android {
	compileSdkVersion 28

	defaultConfig {
		applicationId "pozzo.apps.travelweather"
		minSdkVersion 21
		targetSdkVersion 28
		versionCode 20
		versionName "1.2.6-SNAPSHOT"

		def googleKeys = getKeys('../keystore/google.keys')
		buildConfigField("String", "DIRECTIONS", "\"${googleKeys.directions}\"")
	}

	buildTypes {
		release {
			debuggable false
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			project.ext.set('archivesBaseName', 'travelWeather-' + android.defaultConfig.versionName
					+ '-' + android.defaultConfig.versionCode)
		}
		debug {
			debuggable true
			minifyEnabled false
			testCoverageEnabled true
		}
	}

	sourceSets {
		test.java.srcDirs += 'src/test/kotlin'
		main.java.srcDirs += 'src/main/kotlin'
	}

	dataBinding {
		enabled = true
	}
}

dependencies {
	def arch = "1.1.1"

	//Google
	implementation "com.android.support:design:27.1.1"
	implementation "android.arch.lifecycle:runtime:$arch"
	implementation "android.arch.lifecycle:extensions:$arch"
	implementation 'com.google.firebase:firebase-core:16.0.1'
	implementation "com.google.firebase:firebase-perf:16.1.0"
	implementation 'com.google.firebase:firebase-messaging:17.3.0'
	implementation 'com.google.android.gms:play-services-maps:15.0.1'
	implementation 'com.google.android.gms:play-services-location:15.0.1'
	kapt "android.arch.lifecycle:compiler:$arch"

	//Web requests
	implementation 'com.squareup.retrofit2:retrofit:2.4.0'
	implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
	implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

	//Mint
	implementation "com.splunk.mint:mint:5.0.0"

	//Custom libraries
	implementation 'com.github.pozzoooo:pozzotools:5243fc9a36'
	implementation 'com.github.faruktoptas:FancyShowCaseView:1.0.1'

	//Kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.25.3"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.25.3"

	//di
	implementation 'com.google.dagger:dagger:2.11'
	kapt 'com.google.dagger:dagger-compiler:2.11'
	kaptTest 'com.google.dagger:dagger-compiler:2.11'

	//unit test
	testImplementation 'junit:junit:4.12'
	testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1'
	testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
	testImplementation "org.robolectric:robolectric:3.8"
	testImplementation "android.arch.core:core-testing:$arch"
}

apply plugin: 'com.google.gms.google-services'
